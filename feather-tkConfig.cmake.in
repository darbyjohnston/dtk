@PACKAGE_INIT@

# \todo Is this the correct way to locate the custom Find*.cmake files?
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

include(CMakeFindDependencyMacro)
find_dependency(Threads)
find_dependency(ZLIB)
find_dependency(nlohmann_json)
find_dependency(PNG)
find_dependency(Freetype)
find_dependency(lunasvg)
if(@ftk_UI_LIB@)
    if(@ftk_API@ STREQUAL "GL_4_1" OR
        @ftk_API@ STREQUAL "GL_4_1_Debug" OR
        @ftk_API@ STREQUAL "GLES_2")
        find_dependency(OpenGL)
        find_dependency(glfw3)
    endif()
    set(ftk_API @ftk_API@)
    add_definitions(-Dftk_API_@ftk_API@)
    if(@ftk_API@ STREQUAL "GL_4_1_Debug")
        add_definitions(-Dftk_API_GL_4_1)
    endif()
    if(@ftk_nfd@)
        find_dependency(nfd)
    endif()
    if(@ftk_PYTHON@)
        find_dependency(Python3)
        find_dependency(pybind11)
    endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/feather-tk-resTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/feather-tk-resourceTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/feather-tk-coreTargets.cmake")
if(@ftk_UI_LIB@)
    include("${CMAKE_CURRENT_LIST_DIR}/gladTargets.cmake")
    if(@ftk_API@ STREQUAL "GL_4_1" OR
        @ftk_API@ STREQUAL "GL_4_1_Debug" OR
        @ftk_API@ STREQUAL "GLES_2")
        include("${CMAKE_CURRENT_LIST_DIR}/feather-tk-glTargets.cmake")
    endif()
    include("${CMAKE_CURRENT_LIST_DIR}/feather-tk-uiTargets.cmake")
endif()

# \todo Is this the correct way to add the include directory?
include_directories("@PACKAGE_INCLUDE_INSTALL_DIR@")

check_required_components(feather-tk)
